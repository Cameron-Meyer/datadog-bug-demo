plugins {
    id("java")
    id("jacoco")
    id("jvm-test-suite")
}

group = "org.datadog.issue"
version = "1.0-SNAPSHOT"

repositories {
    mavenCentral()
}

configurations {
    tracingAgent
    tracingAgent.transitive = false
}

tasks.register('copyAgentToLibs', Copy) {
    from configurations.tracingAgent
    into jar.destinationDirectory
    rename { "java-tracing-agent.jar" }
}

tasks.withType(Test).configureEach {
    dependsOn("copyAgentToLibs")
    environment("DD_API_KEY", "FAKE")
    environment("DD_APPLICATION_KEY", "KEY")
    jvmArgs("-javaagent:${copyAgentToLibs.destinationDir.toPath().toString() + File.separator}java-tracing-agent.jar="
            + "dd.civisibility.enabled=true"
            + ",dd.civisibility.agentless.enabled=true"
            + ",dd.service=my-java-app"
            + ",dd.site=us5.datadoghq.com"
            + ",dd.env=ci"
    )
}

dependencies {
    tracingAgent "com.datadoghq:dd-java-agent:1.22.0"

    testImplementation "com.intuit.karate:karate-junit5:1.4.0"
    implementation "com.datadoghq:dd-javac-plugin-client:0.1.7"
    annotationProcessor "com.datadoghq:dd-javac-plugin:0.1.7"
    testAnnotationProcessor "com.datadoghq:dd-javac-plugin:0.1.7"
}

test {
    useJUnitPlatform()
    jacoco.enabled = false
    outputs.upToDateWhen { false }
}

tasks.withType(JavaCompile).configureEach {
    options.compilerArgs.add('-Xplugin:DatadogCompilerPlugin')
}
